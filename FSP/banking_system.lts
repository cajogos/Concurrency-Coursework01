const MAX = 5 // Maximum amount - to avoid too many states
const T_VALUE = 1 // Transaction value
range BAL_RANGE = 0..MAX

// A set containing the two bank account definitions
set StudentAccounts = { rikAcc, sueAcc }

// Bank Account Process - It contains a BALANCE process that holds its current balance
BANK_ACCOUNT(BAL = 3) = BALANCE[BAL], BALANCE[curBal : BAL_RANGE] = (
    acquire -> read[curBal] -> write[newBal : BAL_RANGE] -> release -> BALANCE[newBal]
).

// Student Process - Can both withdraw or deposit
STUDENT = (
    acquire -> read[cur :  BAL_RANGE] -> (
          when(cur > T_VALUE) withdraw -> write[cur - T_VALUE] -> release -> STUDENT
        | when((cur + T_VALUE) <= MAX) deposit -> write[cur + T_VALUE] -> release -> STUDENT
        | write[cur] -> release -> STUDENT
    )
).

// Loan Company Process - Can only deposit
LOAN_COMPANY = (
    acquire -> read[cur : BAL_RANGE] -> (
          when((cur + T_VALUE) <= MAX) deposit -> write[cur + T_VALUE] -> release -> LOAN_COMPANY
        | write[cur] -> release -> LOAN_COMPANY
    )
).

// University Process - Can only withdraw
UNIVERSITY = (
    acquire -> read[cur : BAL_RANGE] -> (
          when(cur > T_VALUE) withdraw -> write[cur - T_VALUE] -> release -> UNIVERSITY
        | write[cur] -> release -> UNIVERSITY
    )
).

// The sharing of the BANK_ACCOUNT process
|| ACCOUNT_SHARE = (
       student : STUDENT
    || company : LOAN_COMPANY
    || university : UNIVERSITY
    || { student, company, university } :: BANK_ACCOUNT(3)
).

// Banking System with both accounts
|| BANK_SYSTEM = (
    StudentAccounts : ACCOUNT_SHARE
).
